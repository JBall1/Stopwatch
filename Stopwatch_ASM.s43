;-------------------------------------------------------------------------------
;   Description: 
;   This code provides a simple stop watch on a MSP430g2553 with custom display
;   attached. The device will start at 00:00. Pressing the right most button 
;   will start the timer, going up by miliseconds. Pressing the middle button at
;   any time will reset the device to 00:00. You can press the left most button
;   at any time to pause the timer. Once the timer reaches 99:90 it will stop on
;   its own and wait for an input from the user. This all is accomplished using
;   both push button interupts for the buttons respectively and using both timers
;   on the board as well as using the dadd instruction to increment the value
;   displayed
;
;   Both Timers (TimerA_0 and TimerA_1) are set up
;   TimerA_0 will be set up to update the display at the rate of one digit
;   every 2 mS.  This results in no observable flicker in the multiplexing. 
;   Slower rates such as 10 mS per digit result in observable flicker.  
;
;   TimerA_1 will be set up to generate the 100 mS interval interrupts
;   for timing for the stopwatch or clock. TimerA_1 has the higher
;   priority of the two timers, so the time update has precedence over display
;   multiplexing. This is also where DisplayValue is incremented using the dadd
;   instruction.
;
;   Target: TI LaunchPad development board with MSP430G2553 device with the
;    custom Display Board installed
;
;       Date:           April 10, 2018
;       Written by:     Joshua Ball
;       Company:        Texas Tech University
;       Adapted from:   DemoStopWatchFrameWork, by Dr. Helm
;       Assembler/IDE:  IAR Embedded Workbench 5.5
;
;       HW I/O assignments:
;       P1.0    (output) Segment A (active low) drives display board
;       P1.1    (output) Segment B (active low) drives display board
;       P1.2    (output) Segment C (active low) drives display board
;       P1.3    (output) Segment D (active low) drives display board
;       P1.4    (output) Segment E (active low) drives display board
;       P1.5    (output) Segment F (active low) drives display board
;       P1.6    (output) Segment G (active low) drives display board
;       P1.7    (output) Segment DP (active low) drives display board
;
;       P2.0    (output) Digit 3 (active low) MSdigit (leftmost)
;       P2.1    (output) Digit 2 (active low)  
;       P2.2    (output) Digit 1 (active low)  
;       P2.3    (output) Digit 0 (active low) LSdigit (rightmost)
;       P2.4    (output) Other - (dots - colon)
;       P2.5    (input)  Pushbutton 0 (active low) (rightmost)
;       P2.6    (input)  Pushbutton 1 (active low) (leftmost)
;       P2.7    (input)  Pushbutton 2 (active low) (middle)
;
;
;
;*******************************************************************************
#include  "msp430g2553.h"
;-------------------------------------------------------------------------------
; Definition of Constants
;-------------------------------------------------------------------------------

;LONG_DELAY      EQU     65535  ; max 16 bit value (FFFFh)
LONG_DELAY      EQU     600  ; 
;LONG_DELAY      EQU     600  ; 
SHORT_DELAY     EQU     5000  ; 

DISPLAY_DELAY   EQU    50  ;


TIMER_A0_COUNT_1   EQU   2000    ; set count value for TimerA_0  
;results in a 2 mS interrupt rate for updating each digit position in the
; display based on 1 MHz SMCLK/1 and counting 2000 of the 1 uS clock events
TIMER_A1_COUNT_1   EQU   50000    ; set count value for TimerA_1  
; results in a 100 mS basic interrupt rate based on 1 MHz SMCLK/2
; this will be a clock rate of one event per 2 uS, counting 50000 of those
; results in one interrupt every 100 mS from this timer

MAX_TIMER_COUNT  EQU    10  ; number of TimerA1 Interrupts that have to occur
; to equal 1 second of elapsed time.

SEG_A         EQU     %00000001 ; Port pin position P1.0
SEG_B         EQU     %00000010 ; Port pin position P1.1
SEG_C         EQU     %00000100 ; Port pin position P1.2
SEG_D         EQU     %00001000 ; Port pin position P1.3
SEG_E         EQU     %00010000 ; Port pin position P1.4
SEG_F         EQU     %00100000 ; Port pin position P1.5
SEG_G         EQU     %01000000 ; Port pin position P1.6
SEG_DP        EQU     %10000000 ; Port pin position P1.7

;since inverted pattern is needed for the display, this defines inverse pattern
SEG_A_N       EQU     ~SEG_A ; Port pin position P1.0
SEG_B_N       EQU     ~SEG_B ; Port pin position P1.1
SEG_C_N       EQU     ~SEG_C ; Port pin position P1.2
SEG_D_N       EQU     ~SEG_D ; Port pin position P1.3
SEG_E_N       EQU     ~SEG_E ; Port pin position P1.4
SEG_F_N       EQU     ~SEG_F ; Port pin position P1.5
SEG_G_N       EQU     ~SEG_G ; Port pin position P1.6
SEG_DP_N      EQU     ~SEG_DP ; Port pin position P1.7

;NOTE: display board requires INVERSE of these patterns due to Active LOW
DIG_3         EQU     %00000001 ; Port pin position P2.0 (MSdigit)
DIG_2         EQU     %00000010 ; Port pin position P2.1
DIG_1         EQU     %00000100 ; Port pin position P2.2
DIG_0         EQU     %00001000 ; Port pin position P2.3(LSdigit)
COL_DG_COM    EQU     %00010000 ; Port pin position P2.4

;since inverted pattern is needed for the display, this defines inverse pattern
DIG_3_N       EQU     ~DIG_3 ; Port pin position P2.0 (MSdigit)
DIG_2_N       EQU     ~DIG_2 ; Port pin position P2.1
DIG_1_N       EQU     ~DIG_1 ; Port pin position P2.2
DIG_0_N       EQU     ~DIG_0 ; Port pin position P2.3(LSdigit)
COL_DG_COM_N  EQU     ~COL_DG_COM ; Port pin position P2.4


;Pushbutton assignments CORRECTED to compensate for board layout swap
PB_0         EQU     %00100000 ; Port pin position P2.5  RightMost button
PB_1         EQU     %10000000 ; Port pin position P2.7  Middle button
PB_2         EQU     %01000000 ; Port pin position P2.6  LeftMost button


SEG_PORT         EQU     P1OUT
DIG_PORT         EQU     P2OUT
PB_PORT          EQU     P2IN

;NOTE: display bd requires the INVERSE of these patterns due to Active LOW
ONE             EQU     %00000110
TWO             EQU     %01011011
THREE           EQU     %01001111
FOUR            EQU     %01100110
FIVE            EQU     %01101101
SIX             EQU     %01111101
SEVEN           EQU     %00000111
EIGHT           EQU     %01111111
NINE            EQU     %01100111
ZERO            EQU     %00111111

;since inverted pattern is needed for the display, this defines inverse pattern
ONE_N           EQU     ~%00000110
TWO_N           EQU     ~%01011011
THREE_N         EQU     ~%01001111
FOUR_N          EQU     ~%01100110
FIVE_N          EQU     ~%01101101
SIX_N           EQU     ~%01111101
SEVEN_N         EQU     ~%00000111
EIGHT_N         EQU     ~%01111111
NINE_N          EQU     ~%01100111
ZERO_N          EQU     ~%00111111


;-------------------------------------------------------------------------------
; Definition of Variables
;-------------------------------------------------------------------------------

            ORG   0x0200     ; beginning of RAM space (necessary statement)
            
NewVar         DB  0
ExampleVar     DB  0
timerGO        DW  0
DecimalOn      DW  0
            
TotalINTCount0 DW  0         ;global variable used by Timer0 ISR  

TotalINTCount1 DW  0         ;global variable used by Timer1 ISR   

DisplayValue  DW  0         ; contains 4 digit value to display in BCD format
                            ; BCDdig3 | BCDdig2  | BCDdig1  | BCDdig0
                            ; xxxx      xxxx       xxxx       xxxx
                            
CurrentDigitPos  DB  0      ;global variable used by WriteDigitToDisplay ISR
                            ; holds digit position of current digit to write
                            
CurrentDigitValue DB 0      ;global variable used by WriteDigitToDisplay ISR
                            ; holds digit value of next digit to write   
                            
Ten_mS          DB  0       ;contains current 10 mS count
Hundred_mS      DB  0       ;contains current 100 mS count
Seconds         DB  0       ;contains current 1 second count
TenSeconds      DB  0       ;contains current 10 second count

CountMode       DB  0       ;Boolean flag, TRUE (1) when counting

PB_0_Mode       DB   0
PB_1_Mode       DB   0
PB_2_Mode       DB   0

;mov.w               #0x0000, &DisplayValue
;-------------------------------------------------------------------------------
            ORG     0xC000                  ; Program Reset (prog memory start)
                                            ; this is Program memory start 
                                            ; address for MSP430G2553 (16K)
;-------------------------------------------------------------------------------
RESET       mov.w   #0x0400,SP        ; Initialize stackpointer(to end of RAM +1)
                                     ; RAM is 512 bytes, last position is 3FFh
                                     
StopWDT     mov.w   #WDTPW+WDTHOLD,&WDTCTL  ; Stop WDT

SetupP1     mov.b   #0xFF, &P1DIR  ; all as outputs          
            mov.b   #0x1F, &P2DIR ; all but 3 MSbits as outputs   
                                                                                        
           ; The PushButtons are wired to P2.5,6,7 (active LOW), so we need to  
           ; turn on the internal Pullup Resistors  - this is done with P2REN
           ; register
           
           mov.b    #PB_0+PB_1+PB_2, &P2REN    ;turn on the internal resistor
           mov.b    #PB_0+PB_1+PB_2, &P2OUT    ; set the resistor to Pullup mode
           
; activate the General Purpose Digital I/O mode for P2.6 and P2.7
           bic.b   #PB_1+PB_2, &P2SEL
           
           ;setup Port 2 interrupts for the pushbuttons
           bis.b   #PB_0+PB_1+PB_2, &P2IE    ; enable interrput for Pushbutton
           bis.b   #PB_0+PB_1+PB_2, &P2IES   ; set edge select for high to low trans           

           
           ; turn off all the segments and digits
           mov.b #0FFh, &SEG_PORT
           mov.b #0FFh, &DIG_PORT  
         
SetupCalibratedClock       
; Set up the clock (calibrated mode at 1 MHz)
; Get the calibrated data for the DCO clock
; Set DCO to 1 MHz:  (this directly from TI Family Guide page283 and 284
          CLR.B &DCOCTL ; Select lowest DCOx  and MODx settings
          MOV.B &CALBC1_1MHZ,&BCSCTL1 ; Set range
          MOV.B &CALDCO_1MHZ,&DCOCTL ; Set DCO step + modulation       
           
SetupTimerA0
            ;these 3 lines of code sets up the first Timer, TimerA_0
            ; note the difference in register names
            ; this one is set up to provide the 2 mS intervals to update
            ; the next digit of the multiplexed display
            mov.w   #TIMER_A0_COUNT_1,&TA0CCR0 ;load a count value into the counter
            mov.w   #CCIE,&TA0CCTL0        ; enable the timer interrupt

            mov.w   #TASSEL_2+ID_0+MC_1,&TA0CTL   ; select SMCLK/1, up mode  
      
SetupTimerA1      
            ;these 3 lines of code sets up the second Timer, TimerA_1
            ; note the difference in register names  
            ; this timer is set up to generate the 100 mS intervals for the
            ; basic clock counting rate  (it has higher priority than TimerA_0
            mov.w   #TIMER_A1_COUNT_1,&TA1CCR0 ;load a count value into the counter
            mov.w   #CCIE,&TA1CCTL0        ; enable the timer interrupt

            mov.w   #TASSEL_2+ID_1+MC_1,&TA1CTL   ; select SMCLK/2, up mode                
      

            mov.w  #0, &TotalINTCount0  ; make sure the total count is zero'ed
            
ClearRAMVariables  
            clr.b   &Ten_mS
            clr.b   &Hundred_mS
            clr.b   &Seconds
            clr.b   &TenSeconds
            clr.b   &CountMode    
            clr.b   &PB_0_Mode      
            clr.b   &PB_1_Mode  
            clr.b   &PB_2_Mode 
            clr.w   &timerGO
            ;clr.w     &DisplayValue
            
            
ClearInterruptFlags
           clr.b   &P1IFG     ; clear the Int flag register for Port 1 
           clr.b   &P2IFG     ; clear the Int flag register for Port 2            
 
 
EnableGeneralInterrupts
            bis.b #GIE,SR          ; enable the general interrupts bit
            ; eint    ; this is another option to do the same thing
            

Done
          jmp Done   ; idle here and wait on Timer interrupts
                      ; Each timer interrupt will update next digit in display

;-------------------------------------------------------------------------------
;           End of main code
;-------------------------------------------------------------------------------                                            
                                           
;-------------------------------------------------------------------------------
;           Subroutines
;------------------------------------------------------------------------------- 

;-------------------------------------------------------------------------------
; WriteNextDigitToDisplay
;  passed in - DisplayValue, CurrentDigitPos 
;  returned - nothing
;  accomplishes - Writes next digit to the expansion bd display
;  uses: R15, global variable CurrentDigitPos, CurrentDigitValue
;-------------------------------------------------------------------------------
WriteNextDigitToDisplay
    push R15     ; save R15 since we use it here
    ;clr.b R15
    
    cmp #0, &CurrentDigitPos
    jeq  WriteDig0
    
    cmp #1, &CurrentDigitPos
    jeq  WriteDig1
    
    cmp #2, &CurrentDigitPos
    jeq  WriteDig2
    
    cmp #3, &CurrentDigitPos
    jeq  WriteDig3
   
    cmp #4, &CurrentDigitPos
    jeq  WriteDig4
    
    ; invalid case - CurrentDigPos is invalid value
    clr &CurrentDigitPos   ; reset it
    jmp DoneWithDigitWriteTotally  ; get out of here
    
WriteDig0
    mov.b #0FFh,DIG_PORT
    mov &DisplayValue, R15  ; make a copy
    and #0x000F, R15         ; mask out all but Dig 0
    ; use the value in R15 as an offset or index into SegPatternTable
    add #SegPatternTable, R15  ; R15 now points to correct Seg pattern to write
    mov.b @R15, SEG_PORT       ; set up the pattern to write
    mov.b #DIG_0_N, DIG_PORT
    jmp DoneWithDigitWrite
    
WriteDig1
    mov.b #0FFh,DIG_PORT
    mov &DisplayValue, R15  ; make a copy
    and #0x00F0, R15         ; mask out all but Dig 1
    rra R15                     ; get the value into LSnibble position
    rra R15
    rra R15
    rra R15
    ; use the value in R15 as an offset or index into SegPatternTable
    add #SegPatternTable, R15  ; R15 now points to correct Seg pattern to write
    mov.b @R15, SEG_PORT       ; set up the pattern to write
    mov.b #DIG_1_N, DIG_PORT
    jmp DoneWithDigitWrite
    
WriteDig2
    mov.b #0FFh,DIG_PORT
    mov &DisplayValue, R15  ; make a copy
    and #0x0F00, R15         ; mask out all but Dig 2
    swpb R15                    ; get the value into LSnibble position
    ; use the value in R15 as an offset or index into SegPatternTable
    add #SegPatternTable, R15  ; R15 now points to correct Seg pattern to write
    mov.b @R15, SEG_PORT       ; set up the pattern to write
    mov.b #DIG_2_N, DIG_PORT
    jmp DoneWithDigitWrite
    
WriteDig3
    mov.b #0FFh,DIG_PORT
    mov &DisplayValue, R15  ; make a copy
    and #0xF000, R15         ; mask out all but Dig 3
    swpb R15
    rra R15                     ; get the value into LSnibble position
    rra R15
    rra R15
    rra R15    
    ; use the value in R15 as an offset or index into SegPatternTable
    add #SegPatternTable, R15  ; R15 now points to correct Seg pattern to write
    mov.b @R15, SEG_PORT       ; set up the pattern to write
    mov.b #DIG_3_N, DIG_PORT
    jmp DoneWithDigitWrite    

WriteDig4
    mov.b #0FFh,DIG_PORT    
    mov #%11111100, R15  ; make a copy 
    ; use the value in R15 as an offset or index into SegPatternTable
    ;add #SegPatternTable, R15  ; R15 now points to correct Seg pattern to write
    mov.b  R15, SEG_PORT       ; set up the pattern to write
    mov.b #COL_DG_COM_N, DIG_PORT
    ;clr.b R12
    jmp DoneWithDigitWrite
    

DoneWithDigitWrite
    inc &CurrentDigitPos   ; set up for next digit position for next time
DoneWithDigitWriteTotally    
    pop R15     ; restore R15 before returning
    ret         ; return
;-------------------------------------------------------------------------------
;  end of WriteNextDigitToDisplay
;-------------------------------------------------------------------------------
;-------------------------------------------------------------------------------
; Delay_Long
;  passed in - nothing
;  returned - nothing
;  accomplishes - long delay
;  uses: R15
;-------------------------------------------------------------------------------
Delay_Long
    push R15     ; save R15 since we use it here
DelayTopL
    mov #LONG_DELAY, R15     ;load loop counter (R15) with Long Delay constant
Loop1Long
    dec R15                     ; decrement loop counter
    jnz Loop1Long               ; Zero yet?, no decrement again

    pop R15     ; restore R15 before returning
    ret         ; return
;-------------------------------------------------------------------------------
;  end of Delay_Long
;-------------------------------------------------------------------------------


;-------------------------------------------------------------------------------
; Button0
;  passed in - nothing
;  returned - nothing
;  accomplishes - moving 1 into timerGO if the button was pressed
;  uses: PB_0,timerGO
;-------------------------------------------------------------------------------
Button0
    mov.w #1, &timerGO
    clr.b   &P2IFG
    jmp Done_here
    ret
;-------------------------------------------------------------------------------
;  end of Button0
;-------------------------------------------------------------------------------
;-------------------------------------------------------------------------------
; Button1
;  passed in - nothing
;  returned - nothing
;  accomplishes - moving 0 into timerGO
;  uses: PB_1,timerGO
;-------------------------------------------------------------------------------
Button1            
             mov #0, timerGO
             clr.b   &P2IFG
             jmp Done_here

ret
;-------------------------------------------------------------------------------
;  end of Button1
;-------------------------------------------------------------------------------

;-------------------------------------------------------------------------------
; Button2
;  passed in - nothing
;  returned - nothing
;  accomplishes - clearing timergo and reseting the device
;  uses: PB_2,DisplayValue,DecimalOn
;-------------------------------------------------------------------------------
Button2 
            mov.w  #0x0000, &DisplayValue  
            clr.w timerGO
            clr.b &PB_2_Mode  ; clear the Boolean flag
            jmp  Done_here   
      ret
;-------------------------------------------------------------------------------
;  end of Button2
;-------------------------------------------------------------------------------



;-------------------------------------------------------------------------------
;           End of all Subroutines
;-------------------------------------------------------------------------------


;-------------------------------------------------------------------------------
;          Interrupt Service Routines
;-------------------------------------------------------------------------------
;-------------------------------------------------------------------------------
; Interrupt Service Routine for Timer_A 1
;  Passed in: nothing
;  Activated every time TimerA_1 times out
;  Uses: updates global variable Hundred_mS, increments DisplayValue if 1 is in
;        timerGO
;-------------------------------------------------------------------------------

TA1_ISR
      cmp.b #1, &timerGO
      jne Done_here
      inc.b &Hundred_mS           ;update basic count increment
      dadd.w  #0x0010, &DisplayValue
      and #0xFFF0, &DisplayValue  
      cmp.w #0x9990,&DisplayValue
      jeq Stop
      reti
    
Stop
    mov #0, &timerGO
    ret    

;-------------------------------------------------------------------------------
;  end of TA0_ISR   Timer_A 0 Interrupt Service Routine
;-------------------------------------------------------------------------------
;-------------------------------------------------------------------------------
; Interrupt Service Routine for Timer_A 0
;  Passed in: nothing
;  Activated every time TimerA_0 times out
;  
;  Uses: nothing directly, but does call WriteNextDigitToDisplay subroutine
;-------------------------------------------------------------------------------

TA0_ISR
      call #WriteNextDigitToDisplay    
      reti    ; return from interrupt


;-------------------------------------------------------------------------------
;  end of TA0_ISR   Timer_A 0 Interrupt Service Routine
;-------------------------------------------------------------------------------
;-------------------------------------------------------------------------------
; Port1_ISR
;  passed in - nothing
;  returned - nothing
;  accomplishes - updates PatternStateFlag
;  uses: nothing
;-------------------------------------------------------------------------------
Port1_ISR
    ; if we get to here, an interrupt occurred on the Port 1
    ; do something here if needed
    
    
DoneWithPort_1_ISR
clr.b &P1IFG    ; clear the flag so system is ready for another interrupt

    reti         ; return from interrupt
;-------------------------------------------------------------------------------
;  end of Port1_ISR
;-------------------------------------------------------------------------------
;-------------------------------------------------------------------------------
; Interrupt Service Routine for Port2_ISR
;  Passed in: nothing
;  Activated every time a pushbutton is pressed 
;  
;  Uses: Checks which push button was pressed and goes to the respective
;        function
;-------------------------------------------------------------------------------
Port2_ISR   
      bit.b #%00100000, &P2IFG   ; was it this Pushbutton?
      jne Button0

      bit.b  #%01000000, &P2IFG   ; was it this Pushbutton?
      jne Button1

      bit.b  #%10000000, &P2IFG   ; was it this Pushbutton?
      jne Button2
      
      ;jmp Done_here
      clr.b &P2IFG    ; clear the flag so system is ready for another interrupt

      
      
Done_here
    reti         ; return from interrupt
;-------------------------------------------------------------------------------
;  end of TA0_ISR   Port2_ISR Interrupt Service Routine
;-------------------------------------------------------------------------------


;-------------------------------------------------------------------------------
;           End of all Interrupt Service Routines
;-------------------------------------------------------------------------------

;-------------------------------------------------------------------------------
;           Definition of Program Data Memory
;-------------------------------------------------------------------------------
            ORG   0xE000   ; this line not strictly necessary, but does force
                            ; the location of this program memory data - this is
                            ; a matter of convenience  - need to leave enough
                            ; space ahead of Interrupt Vectors for the data that
                            ; we are putting here

; usage: use SegPatternTable as base address, then offset into table for
; a specific 7-seg number (pattern) to display
SegPatternTable  ; contains patterns for each 7-seg number
     DB  ZERO_N
     DB  ONE_N
     DB  TWO_N
     DB  THREE_N
     DB  FOUR_N
     DB  FIVE_N
     DB  SIX_N
     DB  SEVEN_N
     DB  EIGHT_N
     DB  NINE_N
     
; usage: use DigitPatternTable as base address, then offset into table for
; a specific digit position to display
DigitPatternTable  ; contains patterns for each digit position common selection
     DB  DIG_0_N
     DB  DIG_1_N
     DB  DIG_2_N
     DB  DIG_3_N
     DB  COL_DG_COM_N


;-------------------------------------------------------------------------------
;           Interrupt Vectors
;-------------------------------------------------------------------------------
            ORG     0xFFF2                  ;TimerA_0 Vector
            DW      TA0_ISR                 ;TimerA_0 Interrupt Service Routine
            
            ORG     0xFFFA                  ;TimerA_1 Vector
            DW      TA1_ISR                 ;TimerA_1 Interrupt Service Routine    
            
            ORG     0xFFE4                  ; MSP430 Port 1 Interrupt Vector
            DW      Port1_ISR               ; address of Port 1 ISR     
            
            ORG     0xFFE6                  ;P2 interupt
            DW      Port2_ISR               ;Address of Port2_ISR               
          
            ORG     0xFFFE                  ; MSP430 RESET Vector
            DW      RESET                   ; establishes the label RESET as 
                                            ; the starting point
            END                             ; END of all code for this program
;-------------------------------------------------------------------------------
;-------------------------------------------------------------------------------
